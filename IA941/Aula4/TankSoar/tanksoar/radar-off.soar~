## Radar-off Operator Proposal
# If the radar is on but no energy, health, missiles and tanks visible,
# then propose the radar-off operator
sp {tanksoar*propose*radar-off
   (state <s> ^name tanksoar
              ^io.input-link <il>)
   (<il> ^radar-status on
        -^radar.<< energy health missiles tank >>) #Tests that there are no radar contacts of these types
-->
   (<s> ^operator <o> +)
   (<o> ^name radar-off
        ^actions.radar.switch off)   
}


## Radar-off Search Control
# If radar-off is proposed, then prefer it to move and turn.
sp {tanksoar*select*radar-off*move
   (state <s> ^name tanksoar
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name radar-off)
   (<o2> ^name << move turn >> )
-->
   (<s> ^operator <o1> > <o2>)
} 
