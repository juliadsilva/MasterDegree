## Turn Operator Proposal
# If the tank is blocked in the forward direction, and not blocked in the
# right or left directions, then propose the turn operator for the unblocked
# direction. Also create an indifferent preference for the operator.
# This operator will also turn on the radar and set the radar-power to 13.
# If the tank is blocked in the forward direction, and in both the right or
# left directions, then propose the turn operator left.
sp {wander*propose*turn
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
    (<b> ^forward yes
         ^ { << left right >> <direction> } no )
-->
   (<s> ^operator <o> + = )
   (<o> ^name turn 
        ^actions <a>)
   (<a> ^rotate.direction <direction>
        ^radar.switch on
        ^radar-power.setting 13)
}


#If front, left and forward directions are blocked
#propose to turn left
sp {wander*propose*turn*backward
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^left yes ^right yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name turn 
        ^actions.rotate.direction left)
}

## Propose Turn Operator
## If the state is named chase and the sound is coming from left or right, turn that direction.
sp {chase*propose*turn
   (state <s> ^name chase
              ^sound-direction {<< left right >> <direction>})
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction <direction>)
}
## Propose Turn Operator Backward
## If the state is named chase and the sound is coming from backward, turn left.
sp {chase*propose*backward
   (state <s> ^name chase
              ^sound-direction backward)
-->
   (<s> ^operator <o> +)
   (<o> ^name turn
        ^actions.rotate.direction left)
}

###Propose Turn Operator
## If the state is attack and there is a tank on radar that right next to the tank, then propose turning in that
## direction and firing.
sp {attack*propose*turn
   (state <s> ^name attack
              ^io.input-link.radar.tank <tank>)
   (<tank> ^distance 0
           ^position { << leaft right >> <dir> })
-->
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions <a>)
   (<a> ^rotate.direction <dir>
        ^fire.weapon missile)
} 
