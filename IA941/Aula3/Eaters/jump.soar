sp {initialize*state*directions
    (state <ss> ^type state)
-->
    (<ss> ^directions <n> <s> <w> <e>)
    (<n> ^value north ^opposite south)
    (<s> ^value south ^opposite north)
    (<w> ^value west ^opposite east)
    (<e> ^value east ^opposite west)
}

#Propose*jump
# If in the cell two steps away in a direction is not a wall,
# propose jump in the direction of that cell, with the cell's content,
# and indicates that this operator can be selected randomly
sp {propose*jump
   (state <s> ^io.input-link.my-location.<dir>.<dir>.content { <co> <> wall })
-->
   (<s> ^operator <o> +, =)
   (<o> ^name jump
        ^direction <dir>
        ^content <co>)
} 

#Apply*jump
# if the jump operator for a direction is selected
# generate an output command to jump in that direction
sp {apply*jump
    (state <s> ^io.output-link <out>
               ^operator <o>)
    (<o> ^name jump
         ^direction <dir>)
-->
    (<out> ^jump.direction <dir>)
}


#Apply*jump*remove-jump
#If the move operator is selected,
# and there is a completed jump command on the outputlink
# then remove that command
sp {apply*jump*remove-jump
    (state <s> ^io.output-link <out>
               ^operator.name jump)
    (<out> ^jump <direction>)
    (<direction> ^status complete)
-->
    (<out> ^jump <direction> -)
}